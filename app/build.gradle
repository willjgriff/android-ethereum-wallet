apply plugin: 'com.android.application'
apply plugin: 'realm-android'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.github.willjgriff.ethereumwallet"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
}

kapt {
    generateStubs = true
}

ext {
    supportLib  = '25.3.1'
    constraint  = '1.0.2'
    timber      = '4.5.1'
    conductor   = '2.0.7'
    rxJava2     = '2.0.4'
    rxAndroid2  = '2.0.1'
    geth        = '1.6.0'
    dagger      = '2.10'
    javax       = '1.0'
    butterknife = '8.4.0'

    junit       = '4.12'
    mockito     = '2.7.0'
    robolectric = '3.2.2'
    kotMockito  = '1.1.0'
    kluent      = '1.14'

    dexmaker    = '2.2.0'
    findbugs    = '3.0.1'
    espresso    = '2.2.2'
}

dependencies {
    // TODO: Remove unused / java dependencies when no longer needed
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:appcompat-v7:${supportLib}"
    compile "com.android.support.constraint:constraint-layout:${constraint}"
    compile "com.android.support:cardview-v7:${supportLib}"
    compile "com.android.support:design:${supportLib}"
    compile "com.jakewharton.timber:timber:${timber}"
    compile "com.bluelinelabs:conductor:${conductor}"
    compile "com.bluelinelabs:conductor-rxlifecycle2:${conductor}"
    compile "io.reactivex.rxjava2:rxjava:${rxJava2}"
    compile "io.reactivex.rxjava2:rxandroid:${rxAndroid2}"
    // Update this once RxJava2 bindings are finalised. Remove maven { url "https://jitpack.io" } from top level build.gradle
    compile "com.github.JakeWharton.RxBinding:rxbinding:31e02dcaca"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile "org.ethereum:geth:${geth}"

    compile "com.google.dagger:dagger:${dagger}"
    provided "javax.annotation:jsr250-api:${javax}"
    kapt "com.google.dagger:dagger-compiler:${dagger}"
    compile "com.jakewharton:butterknife:${butterknife}"
    kapt "com.jakewharton:butterknife-compiler:${butterknife}"

    compile "com.google.code.gson:gson:2.8.0"

    // Leak Canary
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'

    testCompile "junit:junit:${junit}"
    testCompile "org.mockito:mockito-core:${mockito}"
    testCompile "org.robolectric:robolectric:${robolectric}"
    testCompile("com.nhaarman:mockito-kotlin:${kotMockito}", {
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-reflect'
    })
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:${kotlin_version}"
    testCompile "org.amshove.kluent:kluent:${kluent}"

    // AndroidTest version of Mockito
    androidTestCompile "com.linkedin.dexmaker:dexmaker-mockito:${dexmaker}"
    androidTestCompile "com.google.code.findbugs:jsr305:${findbugs}"
    androidTestCompile("com.android.support.test.espresso:espresso-core:${espresso}", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}
